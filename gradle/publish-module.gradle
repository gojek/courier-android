apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

tasks.withType(dokkaHtmlPartial.getClass()).configureEach {
    pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.base.DokkaBase": """{ "separateInheritedMembers": true}"""]
    )
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives androidSourcesJar
    archives javadocJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                artifact javadocJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Courier'
                    url = 'https://github.com/gojek/courier-android'
                    licenses {
                        license {
                            name = 'Courier License'
                            url = 'https://github.com/gojek/courier-android/blob/main/LICENSES/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'deepanshu'
                            name = 'Deepanshu'
                            email = 'deepanshu@gojek.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/gojek/courier-android.git'
                        developerConnection = 'scm:git:ssh://github.com/gojek/courier-android.git'
                        url = 'https://github.com/gojek/courier-android/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    def isLocal = project.hasProperty("IS_LOCAL") && (project.findProperty("IS_LOCAL") == "true")
    if (!isLocal) {
        useInMemoryPgpKeys(
                rootProject.ext["signing.keyId"],
                rootProject.ext["signing.key"],
                rootProject.ext["signing.password"],
        )
        sign publishing.publications
    }
}